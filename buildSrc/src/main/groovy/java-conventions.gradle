plugins {
	id "java"
	id "jacoco"
	id "pmd"
	id "checkstyle"
	id "com.github.spotbugs"
	id "io.freefair.lombok"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
	withSourcesJar()
}

jar {
	dependsOn check
}

check {
	dependsOn jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
	dependsOn test
	violationRules {
		rule {
			limit {
				minimum = jacocoMinRatio as BigDecimal
			}
		}
	}
}

test {
	finalizedBy jacocoTestReport
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.required = true
	}
}

checkstyle {
	configDirectory = file("$rootDir/gradle/config/checkstyle")
	toolVersion = "10.16.0"
}

pmd {
	consoleOutput = true
	toolVersion = "7.1.0"
	ruleSets = ["ruleset.xml"]
}

tasks.withType(Pmd).configureEach {
	pmdClasspath += files("$rootDir/gradle/config/pmd")
}

spotbugs {
	omitVisitors = ["ConstructorThrow", "FindReturnRef"]
	excludeFilter = file("$rootDir/gradle/config/spotbugs/exclude.xml")
}

repositories {
	mavenCentral()
}

dependencies {
	implementation platform(project(":lunch-platform"))

	testImplementation "org.assertj:assertj-core"
	testImplementation "org.junit.jupiter:junit-jupiter"
	testImplementation "org.mockito:mockito-junit-jupiter"
}