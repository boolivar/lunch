plugins {
	id "application"
	id "checkstyle"
	id "pmd"
}

mainClassName = "org.bool.lunch.Main"

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
	withSourcesJar()
}

test {
	useJUnitPlatform()
}

jar {
	manifest {
		attributes("Automatic-Module-Name": "org.bool.lunch.core")
		attributes("Add-Opens": "java.base/jdk.internal.loader=org.bool.lunch.core")
	}
}

checkstyle {
	configDirectory = file("gradle/config/checkstyle")
	toolVersion = "10.12.1"
}

pmd {
	consoleOutput = true
	toolVersion = "6.55.0"
	ruleSets = ["ruleset.xml"]
}

tasks.withType(Pmd).configureEach {
	pmdClasspath += files("gradle/config/pmd")
}

repositories {
	mavenCentral()
}

dependencies {
	ext["akkaVersion"] = "2.5.27"
	ext["scalaVersion"] = "2.12"
	ext["scalecubeVersion"] = "2.8.10"
	ext["mockitoVersion"] = "5.5.0"

	implementation "org.apache.commons:commons-lang3:3.9"
	implementation "org.slf4j:slf4j-api:2.0.9"
	implementation "com.esotericsoftware.yamlbeans:yamlbeans:1.13"
	implementation "com.hierynomus:sshj:0.29.0"
	implementation "com.lightbend.akka.management:akka-management-cluster-http_${scalaVersion}:1.0.6"
	implementation "com.typesafe.akka:akka-cluster-typed_${scalaVersion}:${akkaVersion}"
	
	implementation "io.scalecube:scalecube-services:${scalecubeVersion}"
	implementation "io.scalecube:scalecube-services-bytebuf-codec:${scalecubeVersion}"
	implementation "io.scalecube:scalecube-services-gateway-netty:${scalecubeVersion}"
	implementation "io.scalecube:scalecube-services-discovery:${scalecubeVersion}"
	implementation "io.scalecube:scalecube-services-transport-jackson:${scalecubeVersion}"
	implementation "io.scalecube:scalecube-services-transport-rsocket:${scalecubeVersion}"
	implementation "commons-io:commons-io:2.13.0"

	runtimeOnly "org.springframework.boot:spring-boot-starter-logging:3.1.3"
	runtimeOnly "com.typesafe.akka:akka-slf4j_${scalaVersion}:${akkaVersion}"
	runtimeOnly "org.slf4j:log4j-over-slf4j:1.7.25"

	runtimeOnly ("org.apache.kafka:kafka_2.12:1.1.0") {
		exclude group: "org.slf4j", module: "slf4j-log4j12"
	}
	runtimeOnly ("org.apache.zookeeper:zookeeper:3.4.10") {
		exclude group: "org.slf4j", module: "slf4j-log4j12"
		exclude group: "log4j", module: "log4j"
	}

	testImplementation "org.junit.jupiter:junit-jupiter:5.10.0"
	testImplementation "org.mockito:mockito-core:${mockitoVersion}"
	testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
	testImplementation "org.assertj:assertj-core:3.24.2"
}
